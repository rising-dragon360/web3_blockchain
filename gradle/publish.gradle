apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: "de.marcphilipp.nexus-publish"

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task testJar(type: Jar) {
    archiveClassifier = 'tests'
    from sourceSets.test.output
}

javadoc { options.encoding = 'UTF-8' }

artifacts { archives sourcesJar, javadocJar, testJar }

publishing {
    publications {
        maven(MavenPublication) {
            artifact sourcesJar
            artifact javadocJar
            artifact testJar

            pom {
                name = 'web3j'
                description.set( project.provider( { project.description } ) )
                url = 'https://web3j.io'
                scm {
                    connection = 'scm:git:https://github.com/web3j/web3j.git'
                    url = 'https://github.com/web3j/web3j.git'
                    developerConnection = 'scm:git://github.com/web3j/web3j.git'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'conor10'
                        name = 'Conor Svensson'
                        email = 'conor10@gmail.com'
                    }
                    developer {
                        id = 'antonydenyer'
                        name = 'Antony Denyer'
                        email = 'antony@web3labs.com'
                    }
                }

            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv('OSSRH_USERNAME')
            password = System.getenv('OSSRH_PASSWORD')
        }
    }
}

signing {
    sign publishing.publications.maven
    def signingKey = new File("$rootDir/web3j.asc")
    if(signingKey.exists()) {
        useInMemoryPgpKeys(signingKey.getText('UTF-8'), System.getenv('GPG_PASSPHRASE'))
    }
}
